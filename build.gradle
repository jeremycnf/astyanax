plugins {
    id 'nebula.netflixoss' version '4.0.1'
}

ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

apply from: 'dependency-versions.gradle'
print "Using cassandraShadedVersion=${cassandraShadedVersion}\n"

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'

    group = "com.netflix.${githubProjectName}"

    repositories {
        jcenter()
    }

    javadoc{
        options {
            failOnError = false
        }
    }
}

gradle.taskGraph.whenReady {
    gradle.taskGraph.allTasks.each {
        if(project.properties.get("shaded") == "true"){
            // Execute tasks only for astyanax-shaded-cassandra-all-no-trans when -Pshaded=true
            if(it.project.name != "astyanax-shaded-cassandra-all-no-trans" &&
                    it.project.name != "astyanax"){
                it.onlyIf { false }
            }
        } else {
            // Execute tasks for all modules except astyanax-shaded-cassandra-all-no-trans when shaded property is not true
            if(it.project.name == "astyanax-shaded-cassandra-all-no-trans"){
                it.onlyIf { false }
            }
        }
    }
}
