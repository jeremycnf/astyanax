// Shaded version of cassandra-all without its huge array of transitive dependencies.
// * Provides the few cassandra-all classes astyanax-cassandra actually uses.
// * Eliminates dependency issues caused by cassandra-all transitives which Astyanax doesn't need.
// * Eliminates incompatibility with other libs that want cassandra-all (e.g. cassandra-unit or cassandra-unit-shaded).
//
// Uses shadow plugin to shade this project's dependencies by renaming their packages.
// Modules depending on this specify normal package names in their source.
// The shadow plugin renames this module's class packages when building the jar.

plugins {
    id 'com.github.johnrengelman.plugin-shadow' version '2.0.3'
    id 'java'
}

dependencies {
    compile ("org.apache.cassandra:cassandra-all:$cassandraVersion") {
        // Exclude all those heavy transitive dependencies because Astyanax doesn't use them
       transitive = false
    }
}

shadowJar {
    relocate("org.apache.cassandra", "astyanax.shaded.org.apache.cassandra"){
        exclude 'org.apache.cassandra.thrift'
    }
    archiveName = 'astyanax-shaded-cassandra-all-no-trans.jar'
}

tasks.build.dependsOn(shadowJar)
